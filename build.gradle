plugins {
	id 'java'
	id 'java-library'
	id 'org.springframework.boot' version '3.1.4'
	id 'io.spring.dependency-management' version '1.1.3'

	id 'com.google.protobuf' version "0.9.4"
}

var commonProject = project(commonModule)
var jpaCoreProject = project(jpaCoreModule)

allprojects {
	apply plugin:'java'
	apply plugin:'java-library'
	apply plugin:'org.springframework.boot'
	apply plugin:'io.spring.dependency-management'
	apply plugin:'com.google.protobuf'

	group = 'com.novel'
	version = '0.0.1-SNAPSHOT'

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	// Enable Java Preview (1)
	compileJava {
		options.compilerArgs += [
				'--enable-preview',
				'-Amapstruct.defaultComponentModel=spring'
		]

	}

	// Enable Java Preview (2)
	compileTestJava {
		options.compilerArgs += [
				'--enable-preview',
//                '-Amapstruct.defaultComponentModel=spring'
		]
	}

	// Enable Java Preview (3)
	test {
		useJUnitPlatform()
		jvmArgs([
				'--enable-preview',
//                '-Amapstruct.defaultComponentModel=spring'
				'-Dspring.profiles.active=local'
		])
	}

	repositories {
		mavenCentral()
	}

	test {
		useJUnitPlatform()
	}
}

subprojects {
	dependencies {
		implementation 'org.springframework:spring-web'
		implementation 'org.springframework.boot:spring-boot-starter'
		implementation 'com.fasterxml.jackson.core:jackson-annotations'
		developmentOnly 'org.springframework.boot:spring-boot-devtools'
		annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'


		// Lombok
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'

		// Logging
		implementation 'org.apache.logging.log4j:log4j-api'
		implementation 'org.apache.logging.log4j:log4j-core'

		// Pageable
		implementation 'org.springframework.data:spring-data-commons'
	}
}

project(commonModule) {
	dependencies {
		// NTP
		implementation 'commons-net:commons-net:3.9.0'
	}
}

project(jpaCoreModule) {
	dependencies {
		implementation commonProject
		api 'org.springframework.boot:spring-boot-starter-data-jpa'
	}
}

project(passwordEncoderCore) {
	dependencies {
		implementation 'org.springframework.boot:spring-boot'
		api 'org.springframework.security:spring-security-crypto'

		implementation 'com.google.code.gson:gson:2.10.1'
		implementation 'com.fasterxml.jackson.core:jackson-annotations'
	}
}

project(globalExceptionHandlerModule) {
	dependencies {
		implementation commonProject
	}
}

project(memberGrpcInterfaceCore) {
	dependencies {
		implementation 'io.grpc:grpc-protobuf:1.57.2'
		implementation 'io.grpc:grpc-stub:1.57.2'
		// never update above 2 (protoBuf task doesn't support jakarta package but supports only javax package)
		compileOnly 'jakarta.annotation:jakarta.annotation-api:1.3.5'
	}
	protobuf {
		protoc {
			artifact = 'com.google.protobuf:protoc:3.24.2'
		}
		generatedFilesBaseDir = "$projectDir/src/generated"
		clean {
			delete generatedFilesBaseDir
		}
		plugins {
			grpc {
				artifact = 'io.grpc:protoc-gen-grpc-java:1.57.2'
			}
		}
		generateProtoTasks {
			all()*.plugins {
				grpc{}
			}
		}
	}

	jar {
		enabled = true
		archiveFileName = "member-grpc-interface.jar"
		destinationDirectory = new File("$rootDir/grpc")
	}
}

// ==== bff-web ====
project(bffWebModule) {
	dependencies {
		implementation commonProject
		implementation project(memberGrpcInterfaceCore)

		implementation 'org.springframework.boot:spring-boot-starter-webflux'
		implementation 'net.devh:grpc-client-spring-boot-starter:2.14.0.RELEASE'
	}

	bootJar {
		enabled = true
	}
}


// ==== member-service ====
project(memberServiceModule) {
	dependencies {
		implementation commonProject
		api project(memberApplicationModule)
		api project(memberRdbAdapterModule)
		api project(memberInternalApiModule)

		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.springframework.boot:spring-boot-starter-validation'
		implementation 'org.springframework.boot:spring-boot-starter-data-redis'

		// GSON
		testImplementation 'com.google.code.gson:gson:2.10.1'
	}

	bootJar {
		enabled = true
	}
}

project(memberDomainModule) {
	dependencies {
		implementation commonProject
	}
}

project(memberApplicationModule) {
	dependencies {
		implementation commonProject
		implementation project(passwordEncoderCore)
		api project(memberDomainModule)
		api project(memberReadModelsModule)

		implementation 'org.springframework.security:spring-security-crypto'
		// GSON
		implementation 'com.google.code.gson:gson:2.10.1'
		// AOP
		implementation 'org.springframework.boot:spring-boot-starter-aop'
	}
}

project(memberRdbAdapterModule) {
	dependencies {
		implementation commonProject
		implementation jpaCoreProject
		implementation project(memberApplicationModule)
		// Map Struct
		implementation 'org.mapstruct:mapstruct:1.5.3.Final'
		annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'
		annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
		// MySQL
		runtimeOnly 'com.mysql:mysql-connector-j'
		// Flyway
		implementation 'org.flywaydb:flyway-mysql'
	}
}

project(memberInternalApiModule) {
	dependencies {
		implementation commonProject
		implementation project(passwordEncoderCore)
		implementation project(memberApplicationModule)
		implementation project(memberGrpcInterfaceCore)

		implementation 'org.springframework.boot:spring-boot-starter'
		api 'net.devh:grpc-server-spring-boot-starter:2.14.0.RELEASE'
	}
}